in vec4 light_space_pos;
in vec2 tex_coords;
in vec2 ao_tex_coords;
in vec3 normal_interpo;
in mat3 tbn;

in vec3 norm_world_to_light;
in vec3 norm_world_to_eye;

uniform sampler2D base_texture;
uniform sampler2D norm_texture;
uniform sampler2D spec_texture;
uniform sampler2D ao_texture;
uniform sampler2D shadow_texture;

uniform bool use_lighting;
uniform bool use_specular_highlight;
uniform bool use_normal_map;
uniform bool use_ao_map;

float shadow() {
  vec3 shadow_tex_coords = (light_space_pos.xyz / light_space_pos.w) * 0.5f + 0.5f;

  // TODO: is there a more efficient way to do this?
  if (shadow_tex_coords.x < 0.0f || shadow_tex_coords.x > 1.0f ||
      shadow_tex_coords.y < 0.0f || shadow_tex_coords.y > 1.0f) {
    return 0.0f;
  }

  float depth_in_map = texture(shadow_texture, shadow_tex_coords.xy).r;
  float current_depth = shadow_tex_coords.z;
  return current_depth > depth_in_map ? 1.0f : 0.0f;
}

vec3 mix_colour(vec3 ambient, vec3 diffuse, vec3 specular) {
  float inv_in_shadow = 1.0f - shadow();
  return clamp(ambient + (diffuse + specular) * inv_in_shadow, 0.0, 1.0);
}

vec3 compute_lighting(float ao_strength, float ambient_intensity, float directional_intensity, float shininess) {
  vec3 colour = texture(base_texture, tex_coords).rgb;
  vec3 normal = normal_interpo;

  if (use_normal_map) {
    normal = normalize(tbn * (texture(norm_texture, tex_coords).xyz * 2.0f - 1.0f));
  }

  if (use_lighting) {
    vec3 norm = normalize(normal);
    float diffuse_factor = max(dot(norm, norm_world_to_light), 0.0f) * directional_intensity;
    vec3 diffuse = diffuse_factor * colour;

    vec3 spec = vec3(0.0, 0.0, 0.0);
    if (use_specular_highlight) {
      vec4 s = texture(spec_texture, tex_coords);
      vec3 spec_colour = s.rgb;
      vec3 reflect_dir = reflect(-norm_world_to_light, norm);
      float spec_power = pow(max(dot(norm_world_to_eye, reflect_dir), 0.0), shininess);
      spec = spec_colour * spec_power;
    }

    vec3 ambient = ambient_intensity * colour;

    if (use_ao_map) {
      vec3 ao = texture(ao_texture, ao_tex_coords).rrr;
      ao = mix(vec3(1.0), ao * 2.0, ao_strength);
      ambient *= ao;
    }

    colour = mix_colour(ambient, diffuse, spec);
  }

  return colour;
}